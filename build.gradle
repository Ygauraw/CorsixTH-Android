buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral();
    maven {
        url "http://www.bugsense.com/gradle/"
    }
}
dependencies {
    compile 'com.android.support:support-v4:18.0.+'
    compile 'com.bugsense.trace:bugsense:3.5'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.android.gms:play-services:3.2.+'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.4.0'

    compile fileTree(dir: 'libs', include: '*.jar')
}

Properties signingProperties = new Properties()
signingProperties.load(new FileInputStream(file("signing.properties")))

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    signingConfigs {
        release {
            storeFile file(signingProperties['signing.release.storeFile'])
            storePassword signingProperties['signing.release.storePassword']
            keyAlias signingProperties['signing.release.keyAlias']
            keyPassword signingProperties['signing.release.keyPassword']
        }

    }

    buildTypes {
        release {
            packageNameSuffix ".release"
            signingConfig signingConfigs.release
        }

        beta {
            packageNameSuffix ".beta"
            signingConfig signingConfigs.release
        }

        debug {
            packageNameSuffix ".debug"
        }


        jnidebug.initWith(buildTypes.debug)
        jnidebug {
            packageNameSuffix ".jnidebug"
            jniDebugBuild true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/Java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.

        beta.setRoot('build-types/beta')
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

tasks.withType(com.android.build.gradle.tasks.PackageApplication) {
    pkgTask -> pkgTask.jniDir new File(projectDir, 'libs')
}

task ndkBuild(type: Exec) {
    commandLine 'ndk-build.cmd', '-j'
}
tasks.withType(Compile) {
    compileTask -> compileTask.dependsOn ndkBuild
}
